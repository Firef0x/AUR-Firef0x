# $Id$
# Maintainer: Kyle Keen <keenerd@gmail.com>
pkgname=zeromq
pkgver=4.0.4
pkgrel=4
pkgdesc="Fast messaging system built on sockets.  C and C++ bindings.  aka 0MQ, ZMQ."
arch=('i686' 'x86_64')
url="http://www.zeromq.org"
license=('LGPL')
depends=('gcc-libs' 'util-linux' 'libsodium')
makedepends=('python2')
options=('staticlibs')
source=(http://download.zeromq.org/$pkgname-$pkgver.tar.gz
        https://raw.githubusercontent.com/zeromq/cppzmq/04d289760/zmq.hpp)
md5sums=('f3c3defbb5ef6cc000ca65e529fdab3b'
         '37b74022028bd4dc9253f123fa27da63')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  ./configure prefix=/usr --with-pgm --with-libsodium
  sed -i 's/python$/&2/' foreign/openpgm/build-staging/openpgm/pgm/{Makefile,version_generator.py}
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
  install -Dm644 "$srcdir/zmq.hpp" "$pkgdir/usr/include/zmq.hpp"
}


