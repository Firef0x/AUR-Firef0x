# Maintainer: Christian Hesse <mail@eworm.de>
# Contributor: Pierre Schmitz <pierre@archlinux.de> ([core] package)
# Contributor: Fran√ßois Charette <firmicus@gmx.net>

pkgname=xz-git
pkgver=5.2.0.r3.gd2d4846
pkgrel=1
pkgdesc='Library and command line tools for XZ and LZMA compressed files - git checkout'
arch=('i686' 'x86_64')
url='http://tukaani.org/xz/'
license=('GPL' 'LGPL' 'custom')
depends=('sh')
makedepends=('git')
provides=('lzma' 'lzma-utils' 'xz-utils' 'xz=5.2.0')
replaces=('lzma' 'lzma-utils' 'xz-utils')
conflicts=('lzma' 'lzma-utils' 'xz-utils' 'xz')
options=('!libtool')
source=("${pkgname}-${pkgver}.tar.gz")
sha256sums=('SKIP')

# keep an upgrade path for older installations
PKGEXT='.pkg.tar.gz'

build() {
	cd ${pkgname}-${pkgver}

	./autogen.sh
	./configure --prefix=/usr \
		--disable-rpath \
		--enable-werror
	make
}

check() {
	cd ${pkgname}-${pkgver}

	make check
}

package() {
	cd ${pkgname}-${pkgver}

	make DESTDIR=${pkgdir} install
	install -d -m0755 ${pkgdir}/usr/share/licenses/xz/
	ln -sf /usr/share/doc/xz/COPYING ${pkgdir}/usr/share/licenses/xz/
	ln -sf /usr/share/licenses/common/GPL2/license.txt ${pkgdir}/usr/share/doc/xz/COPYING.GPLv2

	pkgrel=1
}

