# $Id$
# Contributor: Chris Brannon <cmbrannon79@gmail.com>
# Contributor: Geoffroy Carrier <geoffroy@archlinux.org>
# Contributor: Dan McGee <dan@archlinux.org>
# Contributor: Jaroslaw Rosiek <philosoph@interia.pl>
# Contributor: Darwin Bautista <djclue917@gmail.com>
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>
# Maintainer: Florian Pritz <flo@xinu.at>

pkgname=asciidoc
pkgver=8.6.9
pkgrel=2
pkgdesc='Text document format for short documents, articles, books and UNIX man pages.'
arch=('any')
url='http://www.methods.co.nz/asciidoc/'
license=('GPL')
depends=('python2' 'libxslt' 'docbook-xsl')
optdepends=('lilypond: music-filter'
            'imagemagick: music-filter (used in conjunction with lilypond)'
            'source-highlight: source-highlight-filter'
            'dblatex: pdf generation'
            'fop: alternative pdf generation'
            'lynx: text generation'
            'w3m: text generation (alternative to lynx)')
source=("http://downloads.sourceforge.net/${pkgname}/${pkgname}-${pkgver}.tar.gz")
md5sums=('c59018f105be8d022714b826b0be130a')

prepare() {
  cd ${pkgname}-${pkgver}

  # python2 fix
  for file in asciidocapi.py a2x.py asciidoc.py filters/music/music2png.py filters/latex/latex2png.py \
	  filters/code/code-filter.py filters/graphviz/graphviz2png.py; do
	sed -i 's_#!/usr/bin/env python_#!/usr/bin/env python2_' $file
  done
  #sed -i -e 's_sys:python_sys:python2_g' -e 's_sys3:python_sys3:python2_g' xhtml11.conf
  #sed -i 's_sys:python_sys:python2_g' xhtml11-quirks.conf
  #sed -i -e 's/{python}/{python2}/g' \
	  #-e 's#{language@python:py:#{language@python2:py:#' \
	  #filters/source/source-highlight-filter.conf \
	  #html5.conf xhtml11.conf xhtml11-quirks.conf
  #sed -i -e "s#'python'#'python2'#g" filters/code/code-filter.py
}

build() {
  cd ${pkgname}-${pkgver}

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc
}

package() {
  cd ${pkgname}-${pkgver}

  make install DESTDIR=${pkgdir}

  install -Dm644 asciidocapi.py \
    ${pkgdir}/usr/lib/python2.7/site-packages/asciidocapi.py

  # fix FS#21579 - [asciidoc] 8.6.2-2 "asciidoc" table style doesn't work (mismatched python version) 
  #sed -i 's/python/python2/' \
    #${pkgdir}/etc/asciidoc/asciidoc.conf
}
