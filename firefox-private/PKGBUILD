# Maintainer: Firef0x < xFirefo { at } gmail { dot } com>
# Contributor: Firef0x < xFirefo { at } gmail { dot } com>
# Contributor: Kyro <chris-bos-lee@hotmail.com>

_pkgver=30.x

pkgname=firefox-private
pkgver=30.0
pkgrel=1
pkgdesc='Standalone web browser from mozilla.org, built with GCC -O3 SSE2, betterpgo, support for media-libs/gstreamer, and added an environment variable MOZILLA_DISABLE_PLUGINS to firefox.'
url='http://sourceforge.net/projects/libportable/files/Firefox'
arch=('x86_64')
license=('MPL' 'GPL' 'LGPL')
depends=('gtk2'
         'mozilla-common'
         'libxt'
         'startup-notification'
         'mime-types'
         'dbus-glib'
         'alsa-lib'
         'desktop-file-utils'
         'hicolor-icon-theme'
         'libvpx'
         'icu'
         'libevent'
         'nss'
         'hunspell'
         'sqlite')
optdepends=('networkmanager: Location detection via available WiFi networks'
            'gst-plugins-good: h.264 video'
            'gst-libav: h.264 video'
            'libpulse: PulseAudio audio driver')
provides=('firefox=30.0' 'firefox-i18n-zh-cn=30.0')
conflicts=('firefox' 'firefox-private-zh-cn')
options=('!emptydirs')

# If you couldn't get the first source file, replace the first item in array 'source' with the following link
# "http://jaist.dl.sourceforge.net/project/libportable/Firefox/${CARCH}/${_pkgver}/firefox-private-${pkgver}.zh-CN.linux-x86_64.tar.bz2"
source=(
        "http://downloads.sourceforge.net/project/libportable/Firefox/${CARCH}/${_pkgver}/firefox-private-${pkgver}.zh-CN.linux-x86_64.tar.bz2"
        "vendor.js"
        "firefox.desktop"
        "16.png"
        "22.png"
        "24.png"
        "32.png"
        "48.png"
        "64.png"
        "128.png"
        "192.png"
        "256.png"
        "384.png")
sha1sums=('2ba5e95e476a359867cc2e12cca0e9fcf939e2d8'
          'eb5d1640f5312ad01661c4406b196fdc40cfefc3'
          '27a7cd91a9faac2d2b07d729cd0fb3a2d5243629'
          '077c7737036af790eb9ee80102d082facf299606'
          'b74f9a7b72cf6295ebf7dd08734c6797500290aa'
          '345f4b9a9618ff3221f5c725073da1467057ea41'
          '97b320865dbe365139b1ece3c2c01b673ecacff6'
          '6209703d3cd18aede02bd945695095b2a5699c89'
          'a293ac493192e4a93d5360086785b0402f09c385'
          '2270a28c1d8f4aaf865cdd9bfa029a3db8f2ce51'
          '3e314c006819e0553841e0ebffb097e1632d278f'
          'e387bc1179f0551ca9ce308778c51e6632dd7dec'
          'acc9dd95a755d0ecf0f4854b708d6ac92f4f1ceb')

install=${pkgname}.install

changelog=README.md

# Set to 0 if you don't need the default searchplugins
_keep_searchplugins=1

package(){
    cd "${srcdir}"/firefox/

    install -d -m755 "${pkgdir}/usr/lib/firefox"
    mv * "${pkgdir}/usr/lib/firefox"

    # Install missing resource files
    install -d -m755 "${pkgdir}/usr/lib/firefox/browser/defaults/preferences"
    install -Dm644 "${srcdir}/vendor.js" "${pkgdir}/usr/lib/firefox/browser/defaults/preferences/vendor.js"

    install -d -m755 "${pkgdir}/usr/share/applications"
    install -Dm644 "${srcdir}/firefox.desktop" "${pkgdir}/usr/share/applications/firefox.desktop"

    for i in 16 22 24 32 48 64 128 192 256 384; do
        install -d -m755 "${pkgdir}/usr/share/icons/hicolor/${i}x${i}/apps"
        install -Dm644 "${srcdir}/$i.png" "${pkgdir}/usr/share/icons/hicolor/${i}x${i}/apps/firefox.png"
    done

    # Use system-provided dictionaries
    ln -s /usr/share/hunspell "${pkgdir}/usr/lib/firefox/dictionaries"
    ln -s /usr/share/hyphen "${pkgdir}/usr/lib/firefox/hyphenation"

    # Remove default searchplugins if _keep_searchplugins=0
    if [ ${_keep_searchplugins} -eq 0 ]; then
        rm -f "${pkgdir}"/usr/lib/firefox/browser/searchplugins/*
    fi

    # Make symbolic links
    install -d -m755 "${pkgdir}/usr/bin"
    ln -sf /usr/lib/firefox/firefox "${pkgdir}"/usr/bin/firefox
    # Workaround for bug #658850
    # https://bugzilla.mozilla.org/show_bug.cgi?id=658850
    ln -sf /usr/lib/firefox/firefox "${pkgdir}"/usr/lib/firefox/firefox-bin
}

# vim:set ts=4 sw=4 et:
